plugins {
    id 'java-library'
    id 'java'
    id 'io.github.goooler.shadow' version '8.1.7'
}

group = project.property('group')
version = project.property('version')

repositories {
    maven {
        url = uri("https://repo.opencollab.dev/main/")
    }
    maven {
        url 'https://repo.opencollab.dev/maven-releases'
        mavenContent {
            releasesOnly()
        }
    }
    mavenLocal()
    mavenCentral()
}

dependencies {
    // Geyser API - needed for all extensions
    compileOnly('org.geysermc.geyser:api:2.4.2-SNAPSHOT')
    compileOnly 'org.spigotmc:spigot-api:1.21.3-R0.1-SNAPSHOT'
}

shadowJar {
    destinationDirectory = file('../server/plugins/Geyser-Spigot/extensions')
    archiveBaseName.set(project.property('modulename') as String)
    archiveClassifier.set('')
    exclude 'META-INF/*.kotlin_module'
    exclude 'kotlin/**'
    exclude 'org/jetbrains/**'
    exclude 'org/intellij/**'
}

processResources {
    def props = [group: project.property('group'), name: project.property('modulename'), version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('extension.yml') {
        expand props
    }
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(21)) // Set this to match your runtime Java version
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Test) {
    useJUnitPlatform()
}
